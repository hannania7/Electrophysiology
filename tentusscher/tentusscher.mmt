[[model]]

# Initial values
membrane.V = -86.2
FNC.m = 0
FNC.h = 0.75
FNC.j = 0.75
LCC.d = 0
LCC.f = 1
LCC.fca = 1
endo.s = 1
all.r = 0
SDRC.Xs = 0
RDRC.xr1 = 0
RDRC.xr2 = 1
CD.g = 1
CD.Cai = 0.0002
CD.Casr = 0.2
sp.Nai = 11.6
sp.Ki = 138.3

# Engine variables
#
[engine]
time = 0 in [ms] bind time
pace = 0 bind pace

[stimulus]
i_stim = engine.pace * amplitude
amplitude = -52 [pA/pF]

[membrane]
dot(V) = -(Iion + stimulus.i_stim)
    label membrane_potential
    in [mV]
    Cm = 0.185 [uF/cm^2] : Cell capacitance per unit surface area

Iion = (FNC.INa 
        + IRKC.IK1 
        + TOC.Ito 
        + RDRC.IKr 
        + SDRC.IKs 
        + LCC.ICaL 
        + NCEC.INaCa 
        + NKPC.INaK 
        + NKPC.IpCa 
        + NKPC.IpK 
        + BC.IbCa 
        + BC.IbNa)
    in [uA/uF]



# Physical constants
[phys]
R = 8314.472  [J/K/mol] : Gas constant
T = 310   [K] : Temperature
F = 96485.3415 [C/mmol] : Faraday's constant
RTF  = R*T/F
FRT  = F/(R*T)
FFRT = F*F/(R*T)

# Extracellular concentrations
[extra]
Nao = 140 [mM] : Extracellular Na+ concentration
Cao = 2 [mM] : Extracellular Ca2+ concentration
Ko  = 5.4 [mM] : Extracellular K+ concentration

# Maximal conductance
[mc]
GNa = 14.838 [nS/pF] : Maximal INa conductance 
GK1 = 5.405 [nS/pF] : Maximal IK1 conductance 
GtoepiM = 0.294 [nS/pF] : Maximal epicardial Ito conductance
Gtoendo = 0.073 [nS/pF] : Maximal endocardial Ito conductance 
GKr = 0.096 [nS/pF] : Maximal IKr conductance
Gksepiendo = 0.245 [nS/pF] : Maximal epi- and endocardial IKs conductance 
GKsM = 0.062 [nS/pF] : Maximal M cell IKs conductance
GCaL = 0.000175 [cm^3/uF/s] : Maximal ICaL conductance
GpK = 0.0146 [nS/pF] : Maximal IpK conductance 
GpCa = 0.825 [nS/pF] : Maximal IpCa conductance 
GbNa = 0.00029 [nS/pF] : Maximal IbNa conductance 
GbCa = 0.000592 [nS/pF] : Maximal IbCa conductance 

# Maximal
[max]
kNaCa = 1000 [pA/pF] : Maximal INaCa
PNaK = 1.362 [pA/pF] : Maximal INaK
Vmaxup = 0.000425 [mM/ms] : Maximal Iup
arel = 0.016464 [mM/s] : Maximal CaSR-dependent Irel
crel = 0.008232 [mM/s] : Maximal CaSR-independent Irel
Vleak = 8e-5 [1/ms] : Maximal Ileak

# Reversal Potentials
[nernst]
pKNa = 0.03 : Relative IKs permeability to Na+
ENa = phys.RTF * log(extra.Nao / sp.Nai)
Ek = phys.RTF * log(extra.Ko / sp.Ki)
ECa = 0.5 * phys.RTF * log(extra.Cao / CD.Cai) 
EKs = phys.RTF * log((extra.Ko + pKNa * extra.Nao) / (sp.Ki + pKNa * sp.Nai))

# fast_sodium_current
[FNC]
use membrane.V
dot(m) = (m1 - m) / Tm
    m1 = 1 / (1 + exp((-56.86 - V) / 9.03))^2
dot(h) = (h1 - h) / Th
    h1 = 1 / (1 + exp((V + 71.55) / 7.43))^2
dot(j) = (j1 - j) / Tj
    j1 = 1 / (1 + exp((V + 71.55) / 7.43))^2
am = 1 / (1 + exp((-60 - V) / 5))
bm = 0.1 / (1 + exp((V + 35) / 5)) + 0.1 / (1 + exp((V - 50) / 200))
Tm = am * bm
ah = if(V < -40, 0.057 * exp((V + 80) / -6.8), 0)
Bh = if(V < -40, 2.7 * exp(0.079 * V) + 310000 * exp(0.3485 * V), 0.77 / (0.13 * (1 + exp((V + 10.66) / -11.1))))
Th = 1 / (ah + Bh)
aj = if(V < -40, (((-25428) * exp(0.2444 * V) - 6.948e-6 * exp(-0.04391 * V)) * (V + 37.78)) / (1 + exp(0.311 * (V + 79.23))), 0)
Bj = if(V < -40, 0.02424 * exp(-0.01052 * V) / (1 + exp(-0.1378 * (V + 40.14))), 0.6 * exp(0.057 * V) / (1 + exp(-0.1 * (V + 32))))
Tj = 1 / (aj + Bj)
INa = mc.GNa * m^3 * h * j * (V - nernst.ENa)

# L_type_Ca_current
[LCC]
use membrane.V
dot(d) = (d1 - d) / Td
    d1 = 1 / (1 + exp((-5 - V) / 7.5))
dot(f) = (f1 - f) / Tf
    f1 = 1 / (1 + exp((V + 20) / 7))
dot(fca) = if(0.01 * d_fca > 0 and V > -60, 0, d_fca)
    d_fca = (fca1 - fca) / Tfca
    fca1 = (afca + bfca + rfca + 0.23) / 1.46
    afca = 1 / (1 + (CD.Cai / 0.000325)^8)
    bfca = 0.1 / (1 + exp((CD.Cai - 0.0005) / 0.0001))
    rfca = 0.2 / (1 + exp((CD.Cai - 0.00075) / 0.0008))
ad = 1.4 / (1 + exp((-35 - V) / 13)) + 0.25
Bd = 1.4 / (1 + exp((V + 5) / 5))
rd = 1 / (1 + exp((50 - V) / 20))
Td = ad * Bd + rd
Tf = 1125 * exp((V + 27)^2 / -240) + 80 + 165 / (1 + exp((25 - V) / 10)) 
Tfca = 2 [ms]
ICaL = mc.GCaL * d * f * fca * 4 * V * phys.FFRT * (CD.Cai * exp(2 * V * phys.FRT) - 0.341 * extra.Cao) / (exp(2 * V * phys.FRT) - 1)


# Transient Outward Current
[TOC]
use membrane.V
Ito = mc.Gtoendo * all.r * endo.s * (V - nernst.Ek)

# For all cell types
[all]
use membrane.V
r1 = 1 / (1 + exp((20 - V) / 6))
Tr = 9.5 * exp((V + 40)^2 / -1800) + 0.8
dot(r) = (r1 - r) / Tr

# For epicardial and M cells
[epi]
use membrane.V
s = 1
Ts = 85 * exp((V + 45)^2 / -320) + 5 / (1 + exp((V - 20) / 5)) + 3

# For endocardial cells
[endo]
use membrane.V
s1 = 1 / (1 + exp((V + 28) / 5))
Ts = 1000 * exp((V + 67)^2 / -1000) + 8
dot(s) = (s1 - s) / Ts

# Slow Delayed Rectifier Current
[SDRC]
use membrane.V
dot(Xs) = (Xs1 - Xs) / Txs
    Xs1 = 1 / (1 + exp((-5 - V) / 14))
axs = 1100 / sqrt(1 + exp((-10 - V) / 6))
bxs = 1 / (1 + exp((V - 60) / 20))
Txs = axs * bxs
IKs = mc.Gksepiendo * (Xs)^2 * (V - nernst.EKs) 

# Rapid Delayed Rectifier Current
[RDRC]
use membrane.V
dot(xr1) = (xr11 - xr1) / Txr1 
    xr11 = 1 / (1 + exp((-26 - V) / 7))
dot(xr2) = (xr21 - xr2) / Txr2
    xr21 = 1 / (1 + exp((V + 88) / 24))
axr1 = 450 / (1 + exp((-45 - V) / 10))
Bxr1 = 6 / (1 + exp((V + 30) / 11.5))
Txr1 = axr1 * Bxr1
axr2 = 3 / (1 + exp((-60 - V) / 20))
Bxr2 = 1.12 / (1 + exp((V - 60) / 20))
Txr2 = axr2 * Bxr2
IKr = mc.GKr * sqrt(extra.Ko / 5.4) * xr1 * xr2 * (V - nernst.Ek)

# Inward Rectifier K+ Current
[IRKC]
use membrane.V
aK1 = 0.1 / (1 + exp(0.06 * (V - nernst.Ek - 200)))
bK1 = (3 * exp(0.0002 * (V - nernst.Ek + 100)) + exp(0.1 * (V - nernst.Ek - 10))) / (1 + exp(-0.5 * (V - nernst.Ek)))
xK1 = aK1 / (aK1 + bK1)
IK1 = mc.GK1 * xK1 * (V - nernst.Ek)

# Na+/Ca2+ Exchanger Current
[NCEC]
use membrane.V
up = exp(z * V * phys.FRT) * sp.Nai^3 * extra.Cao - exp((z - 1) * V * phys.FRT) * extra.Nao^3 * CD.Cai * y
down = (KmNai^3 + extra.Nao^3) * (KmCa + extra.Cao) * (1 + ksat * exp((z - 1) * V * phys.FRT))
z = 0.35 : Voltage dependence parameter of INaCa 
y = 2.5 : Factor enhancing outward nature of INaCa
KmNai = 87.5 [mM] : Nai half-saturation constant for INaCa
KmCa = 1.38 [mM] : Cai half-saturation constant for INaCa 
ksat = 0.1 : Saturation factor for INaCa 
INaCa = max.kNaCa * (up / down)

# Na+/K+ Pump Current
[NKPC]
use membrane.V
KmK = 1 [mM] : KO half-saturation constant of INaK
KmNa = 40 [mM] : Nai half-saturation constant of INaK
INaK = (max.PNaK * extra.Ko)  / (extra.Ko + KmK) * sp.Nai / (sp.Nai + KmNa) / (1 + (0.1245 * exp(-0.1 * V * phys.FRT)) + 0.0353 * exp(-V * phys.FRT))
IpCa = mc.GpCa * CD.Cai / (KpCa + CD.Cai)
    KpCa = 0.0005 [mM] : Cai half-saturation constant of IpCa
IpK = mc.GpK * (V - nernst.Ek) / (1 + exp((25 - V) / 5.98))

# Background Currents
[BC]
use membrane.V
IbNa = mc.GbNa * (V - nernst.ENa)
IbCa = mc.GbCa * (V - nernst.ECa)

# Calcium Dynamics
[CD]
use membrane.V
Cm = 0.185
gm = if(Cai < 0.00035 , 1 / (1 + (Cai / 0.00035)^6) , 1 / (1 + (Cai / 0.00035)^16))
Tg = 2 [ms]
d_g = (gm-g) / Tg
dot(g) = if(0.01*d_g > 0 and V > -60 , 0 , d_g)
Caibufc = 1 / (1 + (Bufc * Kbufc) / (Cai + Kbufc)^2)
Bufc = 0.15 [mM] : Total cytoplasmic buffer concentration
Kbufc = 0.001 [mM] : Cai half-saturation constant for cytoplasmic buffer
dot(Cai) = Caibufc * (Ileak - Iup + Irel - (LCC.ICaL + BC.IbCa + NKPC.IpCa - (2 * NCEC.INaCa)) / (2 * Vc * phys.F) * Cm)   
Vc = 0.016404 [um^3] : Cytoplasmic volume
Casrbufsr = 1 / (1 + (Bufsr * Kbufsr) / (Casr + Kbufsr)^2)
Bufsr = 10 [mM] : Total sarcoplasmic buffer concentration
Kbufsr = 0.3 [mM] : CaSR half-saturation constant for sarcoplasmic buffer
dot(Casr) = Casrbufsr * Vc / Vsr * (Iup - (Irel + Ileak)) 
Vsr = 0.001094 [um^3] : Sarcoplasmic reticulum volume
Ileak = max.Vleak * (Casr - Cai)
Iup = max.Vmaxup / (1 + (Kup^2 / Cai^2))
    Kup = 0.00025 [mM] : Half-saturation constant of Iup
Irel = (max.arel * (Casr^2) / ((brel)^2 + (Casr)^2) + max.crel) * LCC.d * g
    brel = 0.25 [mM] : CaSR half-saturation constant of Irel

# Sodium and Potassium Dynamics
[sp]
use membrane.V
Cm = 0.185
dot(Nai) = (FNC.INa + BC.IbNa + 3 * NKPC.INaK + 3 * NCEC.INaCa) / -(CD.Vc * phys.F) * Cm 
dot(Ki) = ((IRKC.IK1 + TOC.Ito + RDRC.IKr + SDRC.IKs + NKPC.IpK + stimulus.i_stim) - (2 * NKPC.INaK)) /-(CD.Vc * phys.F) * Cm

[[protocol]]
# Level  Start    Length   Period   Multiplier
1         0         1      1000        0

[[script]]
import matplotlib.pyplot as plt
import myokit

#
# This example file uses the O'Hara-Rudy model in epicardial mode to produce
# and display a single AP.
#

# Get model from magic method
m = get_model()

s = myokit.Simulation(m, p) 
# Run simulation
d = s.run(600)  
# Display the result
plt.figure()
plt.title('tentusser("cai=1e-8, casr=1e-4, Iax=1")')
plt.plot(d['engine.time'], d['membrane.V'])
plt.legend()
plt.show()